apply plugin: 'com.android.application'

android {
    compileSdkVersion 10
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.google.android.stardroid"
        minSdkVersion 10
        targetSdkVersion 10
        versionCode 1202
        versionName "1.7.1"
        buildConfigField 'String', 'GOOGLE_ANALYTICS_CODE', '""'
    }
    signingConfigs {
        release {
            Properties properties = new Properties()
            // Sky Map devs should create this file with the signing passwords
            properties.load(project.file('no-checkin.properties').newDataInputStream())
            storeFile file("stardroid-release-key.keystore")
            storePassword properties.getProperty('store-pwd')
            keyPassword properties.getProperty('key-pwd')
            keyAlias "stardroid"
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            if (file('no-checkin.properties').exists()) {
                signingConfig signingConfigs.release
                Properties properties = new Properties()
                // Sky Map devs should create this file with the Google Analytics key
                properties.load(project.file('no-checkin.properties').newDataInputStream())
                def analyticsKey = properties.getProperty('analytics-key')
                buildConfigField 'String', 'GOOGLE_ANALYTICS_CODE', analyticsKey
            }
        }
    }

    lintOptions {
        // Lint complains because of missing translations.
        // TODO(jontayler): fix the missing translations.
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.easymock:easymock:2.5.2'
}

